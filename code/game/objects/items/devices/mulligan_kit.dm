/obj/item/fake_identity_kit
	name = "fake identity kit"
	desc = "Toda a papelada que você precisa para ter um novo começo e um álibi perfeito, além de um pouco de assistência digital para inserir você nos registros da tripulação."
	icon = 'icons/obj/service/bureaucracy.dmi'
	icon_state = "docs_mulligan"
	w_class = WEIGHT_CLASS_TINY
	interaction_flags_click = NEED_LITERACY|NEED_LIGHT|NEED_DEXTERITY|NEED_HANDS|ALLOW_RESTING
	/// What do we set up our "new arrival" as?
	var/assigned_job = JOB_ASSISTANT

/obj/item/fake_identity_kit/examine_more(mob/user)
	. = ..()
	. += span_info("Usar este kit após exposição ao soro Mulligan criará uma identidade falsa para sua nova aparência.")
	. += span_info("Isso vai adicioná-lo a vários manifestos da estação, criar um cartão de identidade de nível assistente, e anunciar sua chegada pelo rádio.")

/obj/item/fake_identity_kit/attack_self(mob/living/carbon/human/user, modifiers)
	. = ..()
	if (!ishuman(user))
		balloon_alert(user, "Não posso passar como empregado!")
		return
	if (find_record(user.real_name))
		balloon_alert(user, "Os registros já existem!")
		return

	user.temporarilyRemoveItemFromInventory(src)
	user.playsound_local(user, 'sound/items/cards/cardshuffle.ogg', 50, TRUE)

	var/obj/item/card/id/advanced/original_id = user.get_idcard(hand_first = FALSE)
	if (original_id)
		user.temporarilyRemoveItemFromInventory(original_id)

	var/datum/job/job = SSjob.get_job(assigned_job)
	user.mind.set_assigned_role(job)

	var/datum/outfit/job_outfit = job.outfit
	var/id_trim = job_outfit::id_trim
	var/obj/item/card/id/advanced/fake_id = new()

	if (id_trim)
		SSid_access.apply_trim_to_card(fake_id, id_trim)
		shuffle_inplace(fake_id.access)

	fake_id.registered_name = user.real_name
	if(user.age)
		fake_id.registered_age = user.age
	fake_id.update_label()
	fake_id.update_icon()

	var/placed_in = user.equip_in_one_of_slots(fake_id, list(
			LOCATION_ID,
			LOCATION_LPOCKET,
			LOCATION_RPOCKET,
			LOCATION_BACKPACK,
			LOCATION_HANDS,
		), qdel_on_fail = FALSE, indirect_action = TRUE)
	if (isnull(placed_in))
		fake_id.forceMove(user.drop_location())
		to_chat(user, span_warning("Deixe sua nova identidade no chão."))
	else
		to_chat(user, span_notice("You quickly put your new ID card [placed_in]."))

	user.update_ID_card()

	var/mob/living/carbon/human/dummy/consistent/dummy = new() // For manifest rendering, unfortunately
	dummy.physique = user.physique
	user.dna.copy_dna(dummy.dna, COPY_DNA_SE|COPY_DNA_SPECIES)
	user.copy_clothing_prefs(dummy)
	dummy.updateappearance(icon_update = TRUE, mutcolor_update = TRUE, mutations_overlay_update = TRUE)
	dummy.dress_up_as_job(job, visual_only = TRUE, player_client = user.client)

	GLOB.manifest.inject(user, appearance_proxy = dummy)
	QDEL_NULL(dummy)

	if (original_id)
		var/returned_to = user.equip_in_one_of_slots(original_id, list(
			LOCATION_BACKPACK,
			LOCATION_LPOCKET,
			LOCATION_RPOCKET,
			LOCATION_HANDS,
		), qdel_on_fail = FALSE, indirect_action = TRUE)
		if (isnull(returned_to))
			fake_id.forceMove(user.drop_location())
			to_chat(user, span_warning("Você deixa cair seu antigo cartão de identidade no chão."))
		else
			to_chat(user, span_notice("You stash your old ID card [returned_to]."))

	var/obj/item/arrival_announcer/announcer = new(user.drop_location())
	user.put_in_hands(announcer)
	to_chat(user, span_notice("Você rapidamente come as sobras da papelada, deixando apenas o sinalizador usado para anunciar sua chegada na estação."))
	qdel(src)

/obj/item/arrival_announcer
	name = "arrivals announcement signaller"
	desc = "Um sinalizador de rádio que usa um backdoor no sistema de anúncio NT para desencadear um anúncio falso de que você acabou de chegar lá, então se autodestrui."
	icon_state = "signaller"
	inhand_icon_state = "signaler"
	icon = 'icons/obj/devices/new_assemblies.dmi'
	lefthand_file = 'icons/mob/inhands/items/devices_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/items/devices_righthand.dmi'
	interaction_flags_click = NEED_DEXTERITY|NEED_HANDS|ALLOW_RESTING

/obj/item/arrival_announcer/attack_self(mob/living/user, modifiers)
	. = ..()
	if (!isliving(user))
		return

	var/name = user.real_name
	var/datum/record/manifest_data = find_record(name)
	if (isnull(manifest_data))
		balloon_alert(user, "Nenhum registro encontrado!")
		return
	var/job = manifest_data.rank
	if (tgui_alert(user, "Announce arrival of [name] as [job]?", "Are you ready?", list("Yes", "No"), timeout = 30 SECONDS) != "Yes")
		return
	if (QDELETED(src) || !user.can_perform_action(src, interaction_flags_click))
		return

	announce_arrival(user, job, announce_to_ghosts = FALSE)
	do_sparks(1, FALSE, user)
	new /obj/effect/decal/cleanable/ash(user.drop_location())
	user.temporarilyRemoveItemFromInventory(src)
	qdel(src)
