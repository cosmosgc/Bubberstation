#define PATHFINDER_PRE_ANIMATE_TIME (2 SECONDS)

///Pathfinder - Can fly the suit from a long distance to an implant installed in someone.
/obj/item/mod/module/pathfinder
	name = "MOD pathfinder module"
	desc = "Este módulo, trazido pela Engenharia Nakamura, tem dois componentes. O primeiro componente é uma série de propulsores e uma sub-rotina de localização computadorizada instalada na própria unidade de controle do terno, permitindo que ele voe em velocidades de estrada usando as fechaduras de acesso do terno para navegar através da estação, e ser capaz de localizar a segunda parte do sistema, um implante de localização instalado na base da coluna vertebral do usuário, transmitindo sua localização para o terno e permitindo que eles a lembrem para sua pessoa a qualquer momento. O implante é armazenado no módulo e precisa ser injetado em um humano para funcionar. Nakamura Engenharia jura que há freios aéreos."
	icon_state = "pathfinder"
	complexity = 1
	module_type = MODULE_USABLE
	use_energy_cost = DEFAULT_CHARGE_DRAIN * 10
	incompatible_modules = list(/obj/item/mod/module/pathfinder)
	required_slots = list(ITEM_SLOT_BACK|ITEM_SLOT_BELT)
	allow_flags = list(MODULE_ALLOW_INACTIVE|MODULE_ALLOW_UNWORN)
	/// The pathfinding implant.
	var/obj/item/implant/mod/implant
	/// Whether the implant has been used or not
	var/implant_inside = TRUE
	/// The jet icon we apply to the MOD.
	var/image/jet_icon
	/// Allow suit activation - Lets this module be recalled from the MOD.
	var/allow_suit_activation = FALSE // I'm not here to argue about balance
	/// Are we currently travelling?
	var/in_transit = FALSE


/obj/item/mod/module/pathfinder/Initialize(mapload)
	. = ..()
	implant = new(src)
	jet_icon = image(icon = 'icons/obj/clothing/modsuit/mod_modules.dmi', icon_state = "mod_jet", layer = LOW_ITEM_LAYER)


/obj/item/mod/module/pathfinder/Destroy()
	QDEL_NULL(implant)
	return ..()

/obj/item/mod/module/pathfinder/Exited(atom/movable/gone, direction)
	if(gone == implant)
		implant_inside = FALSE
		update_icon_state()
	return ..()

/obj/item/mod/module/pathfinder/update_icon_state()
	. = ..()
	icon_state = implant_inside ? "pathfinder" : "pathfinder_empty"

/obj/item/mod/module/pathfinder/examine(mob/user)
	. = ..()
	if(implant_inside)
		. += span_notice("Use em um humano para implantá-los.")
	else
		. += span_warning("O implante está faltando.")

/obj/item/mod/module/pathfinder/attack(mob/living/target, mob/living/user, list/modifiers, list/attack_modifiers)
	if(!ishuman(target) || !implant_inside) // Not human, or no implant in module
		return
	if(!do_after(user, 1.5 SECONDS, target = target))
		balloon_alert(user, "Interrompido!")
		return
	if(!implant.implant(target, user)) // If implant fails
		balloon_alert(user, "Não posso implantar!")
		return
	if(target == user)
		to_chat(user, span_notice("Você se implantou com [implant]."))
	else
		target.visible_message(span_notice("[user] implante [target]."), span_notice("[user] Implante você com [implant]."))
	playsound(src, 'sound/effects/spray.ogg', 30, TRUE, -6)

/obj/item/mod/module/pathfinder/on_use(mob/activator)
	. = ..()
	if(mod.wearer && implant_inside) // implant them
		try_implant(activator)
		return
	if(mod.wearer)
		balloon_alert(activator, "Terno já usado!")
	else
		recall(activator)


/// Assuming we have a wearer, attempt to implant them.
/obj/item/mod/module/pathfinder/proc/try_implant(mob/activator)
	if(!ishuman(mod.wearer)) // Wearer isn't human
		return
	if(!implant.implant(mod.wearer, mod.wearer))
		balloon_alert(activator, "Não posso implantar!")
		return
	balloon_alert(activator, "implanted")
	if(!(activator == mod.wearer)) // someone else implanted you
		balloon_alert(mod.wearer, "Rastreador implantado!")
	playsound(src, 'sound/effects/spray.ogg', 30, TRUE, -6)

/obj/item/mod/module/pathfinder/proc/attach(mob/living/user)
	if(!ishuman(user))
		return
	var/mob/living/carbon/human/human_user = user
	if(human_user.get_item_by_slot(mod.slot_flags) && !human_user.dropItemToGround(human_user.get_item_by_slot(mod.slot_flags)))
		return
	if(!human_user.equip_to_slot_if_possible(mod, mod.slot_flags, qdel_on_fail = FALSE, disable_warning = TRUE))
		return
	mod.quick_deploy(user)
	human_user.update_action_buttons(TRUE)
	playsound(mod, 'sound/machines/ping.ogg', 50, TRUE)
	drain_power(use_energy_cost)

/obj/item/mod/module/pathfinder/proc/recall(mob/recaller)
	if(!implant)
		balloon_alert(recaller, "Sem implante!")
		return FALSE
	if(recaller != implant.imp_in && !allow_suit_activation) // No pAI recalling
		balloon_alert(recaller, "Usuário inválido!")
		return FALSE
	if(mod.open)
		balloon_alert(recaller, "Cobertura aberta!")
		return FALSE
	if(in_transit)
		balloon_alert(recaller, "Traje em trânsito!")
		return FALSE
	var/atom_on_turf = get_atom_on_turf(mod)
	if(ismob(atom_on_turf))
		if(atom_on_turf == recaller)
			balloon_alert(recaller, "Já está usado!")
		else
			recaller.balloon_alert(recaller, "O terno é usado por outra pessoa!")
		return FALSE

	in_transit = TRUE
	animate(mod, 0.5 SECONDS, pixel_x = base_pixel_y, pixel_y = base_pixel_y)
	mod.Shake(pixelshiftx = 1, pixelshifty = 1, duration = PATHFINDER_PRE_ANIMATE_TIME)
	addtimer(CALLBACK(src, PROC_REF(do_recall), recaller), PATHFINDER_PRE_ANIMATE_TIME, TIMER_DELETE_ME)

	balloon_alert(recaller, "Traje recolhido")
	if(!(recaller == mod.wearer))
		balloon_alert(mod.wearer, "Traje recolhido")
	return TRUE

/// Pod-transport the suit to its owner
/obj/item/mod/module/pathfinder/proc/do_recall(mob/recaller)
	var/container = get_atom_on_turf(mod)
	if(ismob(container))
		balloon_alert(recaller, "Lançamento interrompido!")
		in_transit = FALSE
		return

	if(iscloset(container))
		var/obj/structure/closet/closet = container
		if (!closet.opened)
			if (!closet.open())
				playsound(closet, 'sound/effects/bang.ogg', vol = 50, vary = TRUE)
				closet.bust_open()


	mod.add_overlay(jet_icon)
	playsound(mod, 'sound/vehicles/rocketlaunch.ogg', vol = 80, vary = FALSE)
	var/turf/land_target = get_turf(implant.imp_in)
	var/obj/structure/closet/supplypod/pod = podspawn(list(
		"target" = get_turf(mod),
		"path" = /obj/structure/closet/supplypod/transport/module_pathfinder,
		"reverse_dropoff_coords" = list(land_target.x, land_target.y, land_target.z),
	))

	pod.insert(mod, pod)
	RegisterSignal(pod, COMSIG_SUPPLYPOD_RETURNING, PROC_REF(pod_takeoff))

	if (istype(container, /obj/machinery/suit_storage_unit))
		var/obj/machinery/suit_storage_unit/storage = container
		storage.locked = FALSE
		storage.open_machine()

/// Track when pod has taken off so we don't falsely report the initial landing
/obj/item/mod/module/pathfinder/proc/pod_takeoff(datum/pod)
	SIGNAL_HANDLER
	RegisterSignal(pod, COMSIG_SUPPLYPOD_LANDED, PROC_REF(pod_landed))

/// When the pod landed, we can recall again
/obj/item/mod/module/pathfinder/proc/pod_landed()
	SIGNAL_HANDLER
	in_transit = FALSE
	mod.cut_overlay(jet_icon)
	playsound(mod, 'sound/items/handling/toolbox/toolbox_drop.ogg', vol = 80, vary = FALSE)
	if (implant?.imp_in?.Adjacent(src))
		INVOKE_ASYNC(src, PROC_REF(attach), implant.imp_in)

// ###########
// THE INPLANT
// ###########


/obj/item/implant/mod
	name = "MOD pathfinder implant"
	desc = "Deixa você lembrar de um maiô a qualquer momento."
	actions_types = list(/datum/action/item_action/mod_recall)
	allow_multiple = TRUE // Surgrey is annoying if you loose your MOD
	/// The pathfinder module we are linked to.
	var/obj/item/mod/module/pathfinder/module
	implant_info = "Activated manually. Allows for the recall of a Modular Outerwear Device by the implant owner at any time."
	implant_lore = "Designed by Nakamura Industries, the MOD Pathfinder implant is the receiving end of the MOD pathfinder module, 		designed to be fabricated with its associated host module. It's designed to be implanted near the base of the spine: 		installing it anywhere else may result in the associated suit deploying incorrectly or failing to engage its airbrakes, 		which Nakamura Industries promises are actually present."

/obj/item/implant/mod/Initialize(mapload)
	. = ..()
	if(!istype(loc, /obj/item/mod/module/pathfinder))
		return INITIALIZE_HINT_QDEL
	module = loc

/obj/item/implant/mod/Destroy()
	module = null
	return ..()

/datum/action/item_action/mod_recall
	name = "Recall MOD"
	desc = "Chame um MODsuit em qualquer lugar, um qualquer hora."
	check_flags = AB_CHECK_CONSCIOUS
	background_icon_state = "bg_mod"
	overlay_icon_state = "bg_mod_border"
	button_icon = 'icons/mob/actions/actions_mod.dmi'
	button_icon_state = "recall"
	/// The cooldown for the recall.
	COOLDOWN_DECLARE(recall_cooldown)

/datum/action/item_action/mod_recall/New(Target)
	. = ..()
	if(!istype(Target, /obj/item/implant/mod))
		qdel(src)
		return

/datum/action/item_action/mod_recall/do_effect(trigger_flags)
	var/obj/item/implant/mod/implant = target
	if(!COOLDOWN_FINISHED(src, recall_cooldown))
		implant.balloon_alert(owner, "Em recarga!")
		return
	if(implant.module.recall(owner))
		implant.balloon_alert(owner, "O traje está chegando...")
		COOLDOWN_START(src, recall_cooldown, 5 SECONDS)

/// Special pod subtype we use just to make insertion check easy
/obj/structure/closet/supplypod/transport/module_pathfinder

/obj/structure/closet/supplypod/transport/module_pathfinder/insertion_allowed(atom/to_insert)
	return istype(to_insert, /obj/item/mod/control)

#undef PATHFINDER_PRE_ANIMATE_TIME
