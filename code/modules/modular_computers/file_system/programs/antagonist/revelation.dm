/datum/computer_file/program/revelation
	filename = "revelation"
	filedesc = "Revelation"
	downloader_category = PROGRAM_CATEGORY_DEVICE
	program_open_overlay = "hostile"
	extended_desc = "Este vírus pode destruir o disco rígido do sistema em que é executado. Pode ser ofuscado parecer outro programa não malicioso. Uma vez armado, destruirá o sistema na próxima execução."
	size = 13
	program_flags = PROGRAM_ON_SYNDINET_STORE
	tgui_id = "NtosRevelation"
	program_icon = "magnet"
	var/armed = 0

/datum/computer_file/program/revelation/on_start(mob/living/user)
	. = ..(user)
	if(armed)
		activate()

/datum/computer_file/program/revelation/proc/activate()
	if(computer)
		if(istype(computer, /obj/item/modular_computer/pda/silicon)) //If this is a borg's integrated tablet
			var/obj/item/modular_computer/pda/silicon/modularInterface = computer
			to_chat(modularInterface.silicon_owner,span_userdanger("SISTEMA DETERMINADO/"))
			addtimer(CALLBACK(modularInterface.silicon_owner, TYPE_PROC_REF(/mob/living/silicon/robot/, death)), 2 SECONDS, TIMER_UNIQUE)
			return

		computer.visible_message(span_notice("\The [computer] A tela brilha e alto zumbido elétrico é ouvido."))
		computer.enabled = FALSE
		computer.update_appearance()

		QDEL_LIST(computer.stored_files)

		computer.take_damage(25, BRUTE, 0, 0)

		if(computer.internal_cell && prob(25))
			QDEL_NULL(computer.internal_cell)
			computer.visible_message(span_notice("\The [computer] A bateria explode na chuva de faíscas."))
			do_sparks(3, FALSE, src)

/datum/computer_file/program/revelation/ui_act(action, params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	switch(action)
		if("PRG_arm")
			armed = !armed
			return TRUE
		if("PRG_activate")
			activate()
			return TRUE
		if("PRG_obfuscate")
			var/newname = params["new_name"]
			if(!newname)
				return
			filedesc = newname
			return TRUE


/datum/computer_file/program/revelation/clone()
	var/datum/computer_file/program/revelation/temp = ..()
	temp.armed = armed
	return temp

/datum/computer_file/program/revelation/ui_data(mob/user)
	var/list/data = list()

	data["armed"] = armed

	return data
