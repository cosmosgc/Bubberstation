//All bundles and telecrystals

/datum/uplink_category/bundle
	name = "Bundles"
	weight = 10

/datum/uplink_item/bundles_tc
	category = /datum/uplink_category/bundle
	surplus = 0
	cant_discount = TRUE
	purchasable_from = parent_type::purchasable_from & ~UPLINK_SPY

/datum/uplink_item/bundles_tc/random
	name = "Random Item"
	desc = "Escolher isso vai comprar um item aleatório. Útil se tiver algum TC sobrando ou se ainda não decidiu uma estratégia."
	item = ABSTRACT_UPLINK_ITEM
	cost = 0
	cost_override_string = "Varies"

/datum/uplink_item/bundles_tc/random/purchase(mob/user, datum/uplink_handler/handler, atom/movable/source)
	var/list/possible_items = list()
	for(var/datum/uplink_item/uplink_item as anything in SStraitor.uplink_items)
		if(src == uplink_item || !uplink_item.item)
			continue
		if(!handler.can_purchase_item(user, uplink_item))
			continue
		possible_items += uplink_item

	if(possible_items.len)
		var/datum/uplink_item/uplink_item = pick(possible_items)
		log_uplink("[key_name(user)] purchased a random uplink item from [handler.owner]'s uplink with [handler.telecrystals] telecrystals remaining")
		SSblackbox.record_feedback("tally", "traitor_random_uplink_items_gotten", 1, initial(uplink_item.name))
		handler.purchase_item(user, uplink_item)

/datum/uplink_item/bundles_tc/telecrystal
	name = "1 Raw Telecrystal"
	desc = "Um telecristal em sua forma mais crua e pura, pode ser usado em uplinks ativos para aumentar sua contagem de telecristal."
	item = /obj/item/stack/telecrystal
	cost = 1
	// Don't add telecrystals to the purchase_log since
	// it's just used to buy more items (including itself!)
	purchase_log_vis = FALSE
	purchasable_from = NONE

/datum/uplink_item/bundles_tc/bundle_a
	name = "Syndi-kit Tactical"
	desc = "Syndicate Bundles, também conhecido como Syndi-Kits, são grupos especializados de itens que chegam em uma caixa simples.\
Esses itens valem mais de 25 telecristais, mas você não sabe qual especialização\
Você receberá. Pode conter itens descontinuados e/ou exóticos.\
O Sindicato só fornecerá um Syndi-Kit por agente."
	item = /obj/item/storage/box/syndicate/bundle_a
	cost = 20
	stock_key = UPLINK_SHARED_STOCK_KITS
	purchasable_from = ~(UPLINK_ALL_SYNDIE_OPS | UPLINK_SPY)

/datum/uplink_item/bundles_tc/bundle_b
	name = "Syndi-kit Special"
	desc = "Syndicate Bundles, também conhecido como Syndi-Kits, são grupos especializados de itens que chegam em uma caixa simples.\
Em Syndi-kit Special, você receberá itens usados por famosos agentes do sindicato do passado.\
Coletivamente vale mais de 25 telecristais, o sindicato adora um bom retrocesso.\
O Sindicato só fornecerá um Syndi-Kit por agente."
	item = /obj/item/storage/box/syndicate/bundle_b
	cost = 20
	stock_key = UPLINK_SHARED_STOCK_KITS
	purchasable_from = ~(UPLINK_ALL_SYNDIE_OPS | UPLINK_SPY)

#define TC_VALUE_SURPLUS "%TC_VALUE_SURPLUS%"

/datum/uplink_item/bundles_tc/surplus
	name = "Syndicate Surplus Crate"
	desc = "Uma caixa empoeirada da parte de trás do armazém do Sindicato entregue diretamente a você através do módulo de suprimentos.\
Se os rumores forem verdadeiros, preencherá seu conteúdo baseado em sua reputação atual.\
Os conteúdos são ordenados para sempre valerem a pena." + TC_VALUE_SURPLUS + "TC. O Sindicato só fornecerá um item excedente por agente."
	item = /obj/structure/closet/crate // will be replaced in purchase()
	cost = 20
	purchasable_from = ~(UPLINK_ALL_SYNDIE_OPS | UPLINK_SPY)
	stock_key = UPLINK_SHARED_STOCK_SURPLUS
	/// Value of items inside the crate in TC
	var/crate_tc_value = 40
	/// crate that will be used for the surplus crate
	var/crate_type = /obj/structure/closet/crate

/datum/uplink_item/bundles_tc/surplus/New()
	. = ..()
	desc = replacetext(desc, TC_VALUE_SURPLUS, crate_tc_value)

/// generates items that can go inside crates, edit this proc to change what items could go inside your specialized crate
/datum/uplink_item/bundles_tc/surplus/proc/generate_possible_items(mob/user, datum/uplink_handler/handler)
	var/list/possible_items = list()
	for(var/datum/uplink_item/uplink_item as anything in SStraitor.uplink_items)
		if(src == uplink_item || !uplink_item.item)
			continue
		if(!handler.check_if_restricted(uplink_item))
			continue
		if(!uplink_item.surplus)
			continue
		if(handler.not_enough_reputation(uplink_item))
			continue
		possible_items += uplink_item
	return possible_items

/// picks items from the list given to proc and generates a valid uplink item that is less or equal to the amount of TC it can spend
/datum/uplink_item/bundles_tc/surplus/proc/pick_possible_item(list/possible_items, tc_budget)
	var/datum/uplink_item/uplink_item = pick(possible_items)
	if(prob(100 - uplink_item.surplus))
		return null
	if(tc_budget < uplink_item.cost)
		return null
	return uplink_item

/// fills the crate that will be given to the traitor, edit this to change the crate and how the item is filled
/datum/uplink_item/bundles_tc/surplus/proc/fill_crate(obj/structure/closet/crate/surplus_crate, list/possible_items)
	var/tc_budget = crate_tc_value
	while(tc_budget)
		var/datum/uplink_item/uplink_item = pick_possible_item(possible_items, tc_budget)
		if(!uplink_item)
			continue
		tc_budget -= uplink_item.cost
		new uplink_item.item(surplus_crate)

/// overwrites item spawning proc for surplus items to spawn an appropriate crate via a podspawn
/datum/uplink_item/bundles_tc/surplus/spawn_item(spawn_path, mob/user, datum/uplink_handler/handler, atom/movable/source)
	var/obj/structure/closet/crate/surplus_crate = new crate_type()
	if(!istype(surplus_crate))
		CRASH("crate_type is not a crate")
	var/list/possible_items = generate_possible_items(user, handler)

	fill_crate(surplus_crate, possible_items)

	podspawn(list(
		"target" = get_turf(user),
		"style" = /datum/pod_style/syndicate,
		"spawn" = surplus_crate,
	))
	return source //For log icon

/datum/uplink_item/bundles_tc/surplus/united
	name = "United Surplus Crate"
	desc = "Uma caixa brilhante e grande para ser entregue diretamente a você através do Supply Pod. Tem um mecanismo de bloqueio avançado com um protocolo anti-tamperamento.\
Recomenda-se que você só tente abri-lo, fazendo outro agente comprar uma chave de grade. Unam-se e lutem.\
Rumores de conter uma valiosa variedade de itens baseado em sua reputação atual. Os conteúdos são ordenados para sempre valerem a pena." + TC_VALUE_SURPLUS + "TC.\
O Sindicato só fornecerá um item excedente por agente."
	cost = 20
	item = /obj/structure/closet/crate/secure/syndicrate
	stock_key = UPLINK_SHARED_STOCK_SURPLUS
	crate_tc_value = 100
	crate_type = /obj/structure/closet/crate/secure/syndicrate

/// edited version of fill crate for super surplus to ensure it can only be unlocked with the syndicrate key
/datum/uplink_item/bundles_tc/surplus/united/fill_crate(obj/structure/closet/crate/secure/syndicrate/surplus_crate, list/possible_items)
	if(!istype(surplus_crate))
		return
	var/tc_budget = crate_tc_value
	while(tc_budget)
		var/datum/uplink_item/uplink_item = pick_possible_item(possible_items, tc_budget)
		if(!uplink_item)
			continue
		tc_budget -= uplink_item.cost
		surplus_crate.unlock_contents += uplink_item.item

/datum/uplink_item/bundles_tc/surplus_key
	name = "United Surplus Crate Key"
	desc = "Este dispositivo inconscpico é realmente uma chave que pode abrir qualquer United Surplus Crate. Só pode ser usado uma vez.\
Embora inicialmente projetado para encorajar a cooperação, agentes rapidamente descobriram que você pode virar a chave da caixa sozinho.\
O Sindicato só fornecerá um item excedente por agente."
	cost = 20
	item = /obj/item/syndicrate_key
	purchasable_from = ~(UPLINK_ALL_SYNDIE_OPS | UPLINK_SPY)
	stock_key = UPLINK_SHARED_STOCK_SURPLUS

#undef TC_VALUE_SURPLUS
